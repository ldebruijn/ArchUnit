apply plugin: 'idea'

ext.moduleName = 'com.tngtech.archunit'

dependencies {
    api dependency.slf4j
    implementation dependency.asm
    implementation dependency.guava

    testImplementation dependency.log4j_api
    testImplementation dependency.log4j_core
    testImplementation dependency.log4j_slf4j
    testImplementation dependency.junit4
    testImplementation dependency.junit_dataprovider
    testImplementation dependency.mockito
    testImplementation dependency.assertj
    testImplementation(dependency.assertj_guava) {
        exclude module: 'assertj-core'
        exclude module: 'guava'
    }
}

shadowJar {
    exclude 'META-INF/**'

    dependencies {
        exclude(dependency(dependency.slf4j))
    }
}

/* Besides SLF4J we'll shadow everything we need into the JAR to avoid conflicts with other libraries*/
publishing{
    publications{
        mavenJava{
            pom.withXml {
                def deps = asNode().dependencies.first() // there is only one "dependencies" element
                deps.children().removeIf { dep ->
                    dep.artifactId.text() != 'slf4j-api' || dep.scope.text() != 'compile'
                }
            }
        }
    }
}

addTestJarTo this
configureSlowTestsFor this

def jdk9MainDirs = ['src/jdk9main/java']
def jdk9TestDirs = ['src/jdk9test/java']
sourceSets {
    jdk9main {
        java {
            srcDirs = jdk9MainDirs
        }
        compileClasspath += sourceSets.main.compileClasspath
    }
    jdk9test {
        java {
            srcDirs = jdk9TestDirs
        }
        compileClasspath += sourceSets.test.compileClasspath
    }
}

dependencies {
    jdk9mainImplementation sourceSets.main.output
    jdk9testImplementation sourceSets.test.output
    jdk9testImplementation sourceSets.jdk9main.output

    runtimeOnly sourceSets.jdk9main.output
}

compileJdk9mainJava.with {
    sourceCompatibility = JavaVersion.VERSION_1_9
    targetCompatibility = JavaVersion.VERSION_1_9

    destinationDir = compileJava.destinationDir
}

compileJdk9testJava.with {
    sourceCompatibility = JavaVersion.VERSION_1_9
    targetCompatibility = JavaVersion.VERSION_1_9
}

task jdk9Test(type: Test) {
    testClassesDirs = sourceSets.jdk9test.output.classesDirs
    classpath = sourceSets.jdk9test.runtimeClasspath
}

[jar, test]*.dependsOn compileJdk9mainJava

[test, jdk9Test].each { testTask ->
    addMultiJdkTestsFor project, testTask
}

spotbugsJdk9test.enabled = false

idea {
    module {
        sourceDirs += jdk9MainDirs.collect { file(it) }
        sourceDirs -= jdk9TestDirs.collect { file(it) }
        testSourceDirs += jdk9TestDirs.collect { file(it) }
    }
}
